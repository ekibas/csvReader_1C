
#Область ПеременныеМодуля

&НаКлиенте //Com только на клиенте
Перем Компонента Экспорт;
#КонецОбласти
#Область ПоследовательноеВыполненниеАсинхронныхВызовов

&НаКлиенте
Процедура КаталогНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	WorkSpace = Результат[0];
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСценарийВыбораФайла(Результат = Неопределено, ПараметрыСценария = Неопределено) Экспорт
	
	НачатьОтсчетШагов(ПараметрыСценария);
	
	Оповещение = Новый ОписаниеОповещения("ВыполнитьСценарийВыбораФайла", ЭтаФорма, ПараметрыСценария);
	
	// подключить расширение работы с файлами
	Если ЭтотШагЕщеНеВыполнялся(ПараметрыСценария) Тогда
		НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
		Возврат;
	ИначеЕсли ЭтоРезультатВыполненногоШага(ПараметрыСценария) Тогда
		// если расширение подключилось, перейти к следующему шагу
		Если Результат = Истина Тогда
			// если расширение не подключилось, задать вопрос об его установке
		ИначеЕсли Результат = Ложь Тогда
			ТекстСообщения = "Расширение для работы с файлами не установлено. Установить?";
			ПоказатьВопрос(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
			Возврат;
			// пользователь отказался от установки расширения по работе с файлами
		ИначеЕсли Результат <> КодВозвратаДиалога.Да Тогда
			Возврат;
			// пользователь подтвердил установку расширения по работе с файлами
		ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
			НачатьУстановкуРасширенияРаботыСФайлами();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// открыть диалог выбора файла
	Если ЭтотШагЕщеНеВыполнялся(ПараметрыСценария) Тогда
		ПараметрыСценария.ДиалогВыбораФайла.Показать(Оповещение);
		Возврат;
	КонецЕсли;
	
	//Обработка результата выбора файла
	Если ЭтотШагЕщеНеВыполнялся(ПараметрыСценария) Тогда
		Если Не Результат = Неопределено Тогда
			ЭтаФорма[ПараметрыСценария.ВариантВыбора] = Результат[0];
		КонецЕсли; 
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтсчетШагов(ПараметрыСценария)
	
	Если ПараметрыСценария = Неопределено Тогда
		ПараметрыСценария = Новый Структура;
	КонецЕсли;
	Если НЕ ПараметрыСценария.Свойство("ПредыдущийШаг") Тогда
		// при первом запуске основной процедуры это свойство отстуствует
		ПараметрыСценария.Вставить("ПредыдущийШаг", 0);
	КонецЕсли;
	ПараметрыСценария.Вставить("ТекущийШаг", 0);
КонецПроцедуры

&НаКлиенте
Функция ЭтотШагЕщеНеВыполнялся(ПараметрыСценария)
	ПараметрыСценария.ТекущийШаг = ПараметрыСценария.ТекущийШаг + 1;
	Если ПараметрыСценария.ПредыдущийШаг >= ПараметрыСценария.ТекущийШаг Тогда
		Возврат Ложь;
	КонецЕсли;
	ПараметрыСценария.ПредыдущийШаг = ПараметрыСценария.ТекущийШаг;
	Возврат Истина;
КонецФункции

&НаКлиенте
Функция ЭтоРезультатВыполненногоШага(ПараметрыСценария)
	Возврат ПараметрыСценария.ПредыдущийШаг = ПараметрыСценария.ТекущийШаг;
КонецФункции

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ПутьК_CSV_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок = "Выберите файл для сериализации в ТЗ 1С";
	ДиалогВыбораФайла.Фильтр = "Внешняя компонента (*.csv)|*.csv";
	ПараметрыСценария = Новый Структура;
	ПараметрыСценария.Вставить("ДиалогВыбораФайла", ДиалогВыбораФайла);
	ПараметрыСценария.Вставить("ВариантВыбора","ПутьКCSVфайлу");
	ВыполнитьСценарийВыбораФайла(, ПараметрыСценария);
КонецПроцедуры

#КонецОбласти


#Область  КомандыФормы

&НаКлиенте
Процедура Подключить(Команда)
	ОповещениеВК = Новый ОписаниеОповещения("ПослеПодключенияВК", ЭтаФорма);
	НачатьПодключениеВнешнейКомпоненты(ОповещениеВК,"AddIn.ExtVK");
	//* TODO: 04.03.18  
	//1. Подключение внешней компоненты выполняется с помощью асинхронного метода НачатьПодключениеВнешнейКомпоненты().
	//2. Для обращения к свойствам внешней компоненты следует использовать асинхронные методы НачатьУстановку<ИмяСвойства>() и НачатьПолучение<ИмяСвойства>().
	//3. Вместо обращения к методу компоненты следует использовать асинхронные методы НачатьВызов<ИмяМетода>().
	//Чтобы не путаться будем использовать комбо руск+англ
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияВК(Результат, Параметры)Экспорт
	Если Результат Тогда
		Оповещение = Новый ОписаниеОповещения("ПолучениеСвойстваКомпоненты",ЭтаФорма);
		Компонента = Новый("AddIn.ExtVK");
		Компонента.НачатьПолучениеVersion(Оповещение);
	иначе
		СтрокаСостояния = "";
	КонецЕсли; 
КонецПроцедуры 

&НаКлиенте
Процедура ПолучениеСвойстваКомпоненты(Результат, Параметры)Экспорт
	
	Если Не Результат = Неопределено Тогда
		СтрокаСостояния = СтрШаблон("Компонента подключена[ ver: %1]",Результат);
	КонецЕсли; 
КонецПроцедуры // ПолучениеСвойстваКомпоненты()

&НаКлиенте
Процедура ПрочитатьCSV(Команда)
	Если НЕ Компонента = Неопределено И ЗначениеЗаполнено(ПутьКCSVфайлу) Тогда
		Оповещение = Новый ОписаниеОповещения("ОбращениеКМетодуКомпоненты",ЭтаФорма);
		//Компонента.НачатьВызовВключитьРежимОтладки(Оповещение,Истина);
		МассивТипов = Новый Массив;
		МассивТипов.Добавить("S");
		МассивТипов.Добавить("D");
		МассивТипов.Добавить("N");
		МассивТипов.Добавить("B");
		МассивТипов.Добавить("S");
		
		МассивНазваний = Новый Массив;
		МассивНазваний.Добавить("сolumn1");
		МассивНазваний.Добавить("сolumn2");
		МассивНазваний.Добавить("сolumn3");
		МассивНазваний.Добавить("сolumn4");
		МассивНазваний.Добавить("сolumn5");
		
		МассивП1 = Новый COMSafeArray(МассивТипов,     "VT_BSTR");
		МассивП2 = Новый COMSafeArray(МассивНазваний,  "VT_BSTR");
		
		Компонента.НачатьВызовСоздатьЗаголовок(Оповещение,МассивП1,МассивП2);
		
		МассивФайлов = Новый Массив;
		МассивФайлов.Добавить(ПутьКCSVфайлу);
		МассивП3 = Новый COMSafeArray(МассивФайлов,  "VT_BSTR");
		Компонента.НачатьВызовКонвертироватьCSVвТЗ(Оповещение,МассивП3,ПолучитьИмяВременногоФайла("txt"),";",Истина); 
	иначе
		//* TODO: 04.03.18 
		//Компонента не подключена
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОбращениеКМетодуКомпоненты(Результат, Параметры, ДопПараметры)Экспорт
	Если НЕ Результат = Неопределено Тогда
		чтОбъект = Новый ЧтениеТекста(Результат,КодировкаТекста.UTF8);
		ТекстФайла = чтОбъект.Прочитать();
		чтОбъект.Закрыть();
		СоздатьТЗНаСервере(ТекстФайла);
	КонецЕсли; 	
КонецПроцедуры  
 
#КонецОбласти

#Область  ВспомогательныеМетоды

&НаСервере
Процедура СоздатьТЗНаСервере(ТекстФайла)

ТЗ = ЗначениеИзСтрокиВнутр(ТекстФайла);	

СформироватьРеквизиты(ТЗ);

КоллекцияCSV.Загрузить(ТЗ);
КонецПроцедуры // СоздатьТЗНаСервере()

&НаСервере
Процедура СформироватьРеквизиты(ТЗ)
	#Область ДобавлениеРеквизитовФормы
	// Массив для новых реквизитов
	ДобавляемыеРеквизиты	= Новый Массив;
	
	Для каждого Колонка Из ТЗ.Колонки Цикл
		Реквизит_Ячейка = Новый РеквизитФормы( Колонка.Имя,Колонка.ТипЗначения,"КоллекцияCSV", Колонка.Заголовок);
		ДобавляемыеРеквизиты.Добавить(Реквизит_Ячейка);
	КонецЦикла;
	
	// Добавим новые реквизиты в форму
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	#КонецОбласти
	
	#Область ДобавлениеЭлементовФормы
	Для каждого Колонка Из ТЗ.Колонки Цикл
		НаименованиеРеквизита = Колонка.Имя;
		НовыйЭлемент = Элементы.Добавить(НаименованиеРеквизита, Тип("ПолеФормы"), Элементы["КоллекцияCSV"]);
		НовыйЭлемент.ПутьКДанным    = "КоллекцияCSV"+"."+НаименованиеРеквизита;
		НовыйЭлемент.Вид            = ВидПоляФормы.ПолеВвода;		
	КонецЦикла;
	#КонецОбласти
	
КонецПроцедуры // СформироватьРеквизиты()

#КонецОбласти




